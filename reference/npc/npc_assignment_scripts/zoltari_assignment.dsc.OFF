zoltari_assignment:
    type: assignment
    debug: false
    actions:
        on assignment:
        - trigger name:click state:true
        - trigger name:chat state:true
        - trigger name:proximity state:true radius:2
    interact scripts:
    - zoltari_interact

    default constants:
        appearance1: "<npc.name> is of the Raven Clan, but has a different headdress on than the rest of his clan."
        appearance2: "He has an odd stare that seems to look past you and he mumbles gibberish once and awhile."
        appearance3: "You sense that <npc.name> has been out on this island far too long and it is affecting his mental and social abilities."
        appearance4: "You feel as though <npc.name> is much more important than his lowly appearance."
        appearance5: "Even though <npc.name> has been malnourished and sometimes speaks nonsense, you realize he was once a great man, which comes through in his keen eyes, and occassional educated speech."
        has_familiarity: true
        sell: <list[apple[quantity=8]|bread|cake|potato|scroll_01[display=Gust Scroll;flag=bound_spell:gust;flag=price:30]|scroll_01[display=Magic Armor Scroll;flag=bound_spell:magic_armor;flag=price:30]|scroll_01[display=Aquaorbis Scroll;flag=bound_spell:aquaorbis;flag=price:40]]>
        buy: <list[iron_sword|<item[scroll_01]>]>

zoltari_interact:
    type: interact
    steps:
        1:
            proximity trigger:
                entry:
                    script:
                    - if <player.has_flag[npc.<npc.name>.prox_count].not> || <player.flag[npc.<npc.name>.prox_count]> == 0:
                        - inject description_inject_task
                        - random:
                            - narrate "I'm here just hawking my wares." targets:<player>
                            - narrate "Would it trouble you to chat with this old mortal?" targets:<player>
                    - else:
                        - if <util.random.int[0].to[10]> == 10:
                            - random:
                                - narrate "Hello again fair traveler." targets:<player>
                                - narrate "On top of Old Spikey, all covered with snow. . ." targets:<player>
                                - narrate "Buy a whiskey for this old fool?" targets:<player>

                    - flag <player> npc.<npc.name>.prox_count:++

                # exit:
                #     script:
                #     - run reset_zoltari_gaze
            click trigger:
                script:
                - inject description_inject_task
                - if <player.item_in_hand> == <item[whiskey].with_flag[whiskey_from_risdoni]>:
                    - flag <npc> suspend_vendor expire:10s
                    - ^run zoltari_risdoni_whiskey_task
                - else:
                    - zap 2 vendor_interact player:<player>
                    - wait 1s
                    - run zoltari_first_question_set
              
        2:
            chat trigger:
                1:
                    trigger: <&7>/1/. I would like to talk to you Zoltari.
                    script:
                    - inject zoltari_task_01
                    - zap 1
        3:
            chat trigger:
                1:
                    trigger: <&7>/1/. Why are you sitting out here Zoltari?
                    script:
                    - inject zoltari_task_02
                    - zap 1
        4:
            chat trigger:
                1:
                    trigger: <&7>/1/. What services do you exchange for the Inn owners?
                    script:
                    - inject zoltari_task_03
                    - zap 1
                2:
                    trigger: <&7>/2/. What adventures have you gone on?
                    script:
                    - inject zoltari_task_04
                    - zap 1
        5:
            chat trigger:
                1:
                    trigger: <&7>/1/. You know magery? Tell me more about it.
                    script:
                    - inject zoltari_task_05
                    - zap 1
                2:
                    trigger: <&7>/2/. Thanks for the stories. I need to move on.
                    script:
                    - inject zoltari_task_06
                    - zap 1
        
        6:
            chat trigger:
                1:
                    trigger: <&7>/1/. I started to become a mage. Can you teach me more?
                    script:
                    - inject zoltari_task_07
                    - zap 1
                2:
                    trigger: <&7>/2/. Can you teach me the ways of magery?
                    script:
                    - inject zoltari_task_08
                    - zap 1
        7:
            chat trigger:
                1:
                    trigger: <&7>/1/. Yes, please teach me the Magic Armor spell.
                    script:
                    - inject zoltari_task_09
                    - zap 1
                2:
                    trigger: <&7>/2/. No, not right now.
                    script:
                    - inject zoltari_task_06
                    - zap 1
        8:
            chat trigger:
                1:
                    trigger: <&7>/1/. Yes, please teach me the Magic Missile spell.
                    script:
                    - inject zoltari_task_09
                    - zap 1
                2:
                    trigger: <&7>/2/. No, not right now.
                    script:
                    - inject zoltari_task_06
                    - zap 1
        9:
            chat trigger:
                1:
                    trigger: <&7>/1/. Yes, I am sure I want to become a mage.
                    script:
                    - inject zoltari_task_11
                    - zap 1
                2:
                    trigger: <&7>/2/. No, not yet.
                    script:
                    - inject zoltari_task_06
                    - zap 1
                    
              
    
zoltari_first_question_set:
    type: task
    script:
    - clickable <npc.name>_task_01 save:<npc.name>_clickable_01 until:30s usages:1 for:<player>
   
    # - narrate <&nl> targets:<player>
    - narrate <&nl>
    - narrate  "<element[<&hover[Click to say this to <npc.name>.]><&color[#545454]><&lb><&color[#00cb43]><&sp><&sp><&color[#fbfb54]>1. <&color[#6fd6cb]>I would like to talk to you Zoltari.<&color[#545454]><&rb><&end_hover>].on_click[<entry[<npc.name>_clickable_01].command>]>" target:<player>
    - zap 2 zoltari_interact player:<player> duration:14s


zoltari_risdoni_whiskey_task:
    type: task
    script:
    - take <player.item_in_hand>

    - narrate "What is this?"
    - wait 1s
    - narrate "Is this from Risdoni?"
    - flag <player> risdoni_zoltari_whiskey
    - wait 2s
    - narrate "Being kind to the citizens of Bardtown will get you far young traveler."

zoltari_task_01:
    type: task
    script:
    - if <player.has_flag[suspend_clickable].not>:
        - zap 1 zoltari_interact player:<player> duration:14s
        - flag <player> suspend_clickable
        - inject description_inject_task
        - random:
            - narrate "Oh goodie. I like visitors." targets:<player>
            - narrate "Zoltari likes a good chat." targets:<player>
            - narrate "Thanks for your kindness to an old mortal." targets:<player>
        - flag <player> suspend_clickable:!
        - clickable <npc.name>_task_02 save:<npc.name>_clickable_02 until:30s usages:1 for:<player>
        - narrate  "<element[<&hover[Click to say this to <npc.name>.]><&color[#545454]><&lb><&color[#00cb43]><&sp><&sp><&color[#fbfb54]>1. <&color[#6fd6cb]>Why are you sitting out here Zoltari?<&color[#545454]><&rb><&end_hover>].on_click[<entry[<npc.name>_clickable_02].command>]>" target:<player>
        - zap 3 zoltari_interact player:<player> duration:14s


zoltari_task_02:
    type: task
    script:
    - if <player.has_flag[suspend_clickable].not>:
        - zap 1 zoltari_interact player:<player> duration:14s
        - flag <player> suspend_clickable
        - inject description_inject_task
        - narrate "The owners of <&color[#3ca91a]>The Plucky Pig Inn<&r>, this fine establishment here, let me plop down outside in exchange for services." targets:<player>
        - wait 3s
        - narrate "These Inns and Taverns are great gathering places to meet people and find gossip of fame and fortune out there in The Wild."
        - wait 3s
        - narrate "I don't get out on adventures too much anymore and am content to sit here and talk to passersby."
        - flag <player> zoltari_intro
        - clickable <npc.name>_task_03 save:<npc.name>_clickable_03 until:30s usages:1 for:<player>
        - clickable <npc.name>_task_04 save:<npc.name>_clickable_04 until:30s usages:1 for:<player>
        - if <player.has_flag[zoltari_exchange]> && <player.has_flag[zoltari_magic_spell]> && <player.has_flag[zoltari_magic_class]>:
            - narrate  "<element[<&hover[Click to say this to <npc.name>.]><&color[#545454]><&lb><&color[#888888]><&sp><&sp>1. What services do you exchange for the Inn owners?<&color[#545454]><&rb><&end_hover>].on_click[<entry[<npc.name>_clickable_03].command>]>" target:<player>
        - else:
            - narrate  "<element[<&hover[Click to say this to <npc.name>.]><&color[#545454]><&lb><&color[#00cb43]><&sp><&sp><&color[#fbfb54]>1. <&color[#6fd6cb]>What services do you exchange for the Inn owners?<&color[#545454]><&rb><&end_hover>].on_click[<entry[<npc.name>_clickable_03].command>]>" target:<player>
        - if <player.has_flag[zoltari_adventures_01]>:
            - narrate  "<element[<&hover[Click to say this to <npc.name>.]><&color[#545454]><&lb><&color[#888888]><&sp><&sp>2. What adventures have you gone on?<&color[#545454]><&rb><&end_hover>].on_click[<entry[<npc.name>_clickable_04].command>]>" target:<player>
        - else:
            - narrate  "<element[<&hover[Click to say this to <npc.name>.]><&color[#545454]><&lb><&color[#00cb43]><&sp><&sp><&color[#fbfb54]>2. <&color[#6fd6cb]>What adventures have you gone on?<&color[#545454]><&rb><&end_hover>].on_click[<entry[<npc.name>_clickable_04].command>]>" target:<player>
        - flag <player> suspend_clickable:!
        - zap 4 zoltari_interact player:<player> duration:14s

zoltari_task_03:
    type: task
    script:
    - if <player.has_flag[suspend_clickable].not>:
        - zap 1 zoltari_interact player:<player> duration:14s
        - flag <player> suspend_clickable
        - inject description_inject_task
        - narrate "I teach Nimbobby and Humbrill's wee one the ropes of beginning magery." targets:<player>
        - wait 3s
        - narrate "And in exchange they give me a place to sit and some food and whiskey." targets:<player>
        - wait 3s
        - narrate format:npcwhisper "but between you and me, he is too young and his lessons are not going well."
        - flag <player> zoltari_exchange
        - clickable <npc.name>_task_05 save:<npc.name>_clickable_05 until:30s usages:1 for:<player>
        - clickable <npc.name>_task_06 save:<npc.name>_clickable_06 until:30s usages:1 for:<player>
        - if <player.has_flag[zoltari_magic]> && <player.flag[class.type]> == mage && <player.has_flag[zoltari_magic_spell]>:
            - narrate  "<element[<&hover[Click to say this to <npc.name>.]><&color[#545454]><&lb><&color[#888888]><&sp><&sp>1. You know magery? Tell me more about it.<&color[#545454]><&rb><&end_hover>].on_click[<entry[<npc.name>_clickable_05].command>]>" target:<player>
        - else:
            - narrate  "<element[<&hover[Click to say this to <npc.name>.]><&color[#545454]><&lb><&color[#00cb43]><&sp><&sp><&color[#fbfb54]>1. <&color[#6fd6cb]>You know magery? Tell me more about it.<&color[#545454]><&rb><&end_hover>].on_click[<entry[<npc.name>_clickable_05].command>]>" target:<player>
        - narrate  "<element[<&hover[Click to say this to <npc.name>.]><&color[#545454]><&lb><&color[#00cb43]><&sp><&sp><&color[#fbfb54]>2. <&color[#6fd6cb]>Thanks for the stories. I need to move on.<&color[#545454]><&rb><&end_hover>].on_click[<entry[<npc.name>_clickable_06].command>]>" target:<player>
        - flag <player> suspend_clickable:!
        - zap 5 zoltari_interact player:<player> duration:14s

zoltari_task_04:
    type: task
    script:
    - if <player.has_flag[suspend_clickable].not>:
        - zap 1 zoltari_interact player:<player> duration:14s
        - flag <player> suspend_clickable
        - inject description_inject_task
        - narrate "Oh back in the old days, I used to be the wizard for the Hell Delvers Society, you may have heard of it. . . out of Druvvenrog?" targets:<player>
        - wait 3s
        - narrate "but that was long ago and we've disbanded." targets:<player>
        - wait 2s
        - narrate "They say I talk to much." targets:<player>
        - flag <player> zoltari_adventures_01
        - if <player.item_in_hand.scriptname> == whiskey:
            - take <player.item_in_hand>
            - narrate "Oooh! Thank you."
            - narrate format:game_message "Zoltari drinks the whiskey" targets:<player>
            - wait 2s
            - narrate "Our party was small but mighty." targets:<player>
            - wait 2s
            - narrate "but you don't want to listen to an old fogey talk about the good old days." targets:<player>
            - flag <player> zoltari_adventures_02
        - flag <player> suspend_clickable:!

zoltari_task_05:
    type: task
    script:
    - if <player.has_flag[suspend_clickable].not>:
        - zap 1 zoltari_interact player:<player> duration:14s
        - flag <player> suspend_clickable
        - inject description_inject_task
        - narrate "Ah The Arcane Arts. . . The deep veins of Deluvia are full of magical energy us mortals can tap." targets:<player>
        - wait 2s
        - narrate "And the old ways of magery seem to be gaining appreciation again after the Great Wars." targets:<player>
        - wait 3s
        - narrate "I excelled in the ways of elemental magery, back in my prime, but have now been reduced to babysitting toddlers."
        - wait 3s
        - narrate "The Four Arch Mages of the Song Tide now preside as the master of magic in this realm."
        - wait 2s
        - narrate <&nl>
        - flag <player> suspend_clickable:!
        - flag <player> zoltari_magic
        - zap 6 zoltari_interact player:<player> duration:14s
       
zoltari_task_07:
    type: task
    script:
    - if <player.has_flag[suspend_clickable].not>:
        - zap 1 zoltari_interact player:<player> duration:14s
        - inject description_inject_task
        - narrate "I sensed you were studying the arcane." targets:<player>
        - wait 2s
        - narrate "It excites me to have someone be interested in this. Almost enough to get up and delve into the ancient dungeons for treasure." targets:<player>
        - wait 3s
        - if <player.has_flag[skill.spells.magic_armor.level].not>:
            - narrate "But for now I will just teach you a spell." targets:<player>
            - wait 2s
            - narrate "Would you like to learn Magic Armor, a defensive spell that gives you some resistance against physical attacks?" targets:<player>
            - clickable <npc.name>_task_09 save:<npc.name>_clickable_09 until:30s usages:1 for:<player>
            - clickable <npc.name>_task_06 save:<npc.name>_clickable_06 until:30s usages:1 for:<player>
            - narrate  "<element[<&hover[Click to say this to <npc.name>.]><&color[#545454]><&lb><&color[#00cb43]><&sp><&sp><&color[#fbfb54]>1. <&color[#6fd6cb]>Yes, please teach me the Magic Armor spell.<&color[#545454]><&rb><&end_hover>].on_click[<entry[<npc.name>_clickable_09].command>]>" target:<player>
            - narrate  "<element[<&hover[Click to say this to <npc.name>.]><&color[#545454]><&lb><&color[#00cb43]><&sp><&sp><&color[#fbfb54]>2. <&color[#6fd6cb]>No, not right now.<&color[#545454]><&rb><&end_hover>].on_click[<entry[<npc.name>_clickable_06].command>]>" target:<player>
            - zap 7 zoltari_interact player:<player> duration:14s
        - else if <player.has_flag[skill.spells.magic_missile.level].not>:
            - narrate "But for now I will just teach you a spell." targets:<player>
            - wait 2s
            - narrate "Would you like to learn Magic Missile, an offensive spell that launches a magical object to damage your target?" targets:<player>
            - clickable <npc.name>_task_10 save:<npc.name>_clickable_10 until:30s usages:1 for:<player>
            - clickable <npc.name>_task_06 save:<npc.name>_clickable_06 until:30s usages:1 for:<player>
            - narrate  "<element[<&hover[Click to say this to <npc.name>.]><&color[#545454]><&lb><&color[#00cb43]><&sp><&sp><&color[#fbfb54]>1. <&color[#6fd6cb]>Yes, please teach me the Magic Armor spell.<&color[#545454]><&rb><&end_hover>].on_click[<entry[<npc.name>_clickable_09].command>]>" target:<player>
            - narrate  "<element[<&hover[Click to say this to <npc.name>.]><&color[#545454]><&lb><&color[#00cb43]><&sp><&sp><&color[#fbfb54]>2. <&color[#6fd6cb]>No, not right now.<&color[#545454]><&rb><&end_hover>].on_click[<entry[<npc.name>_clickable_06].command>]>" target:<player>
            - zap 8 zoltari_interact player:<player> duration:14s
        - else:
            - narrate "Alas I don't have anything to teach you right now. Perhaps if you come back later" targets:<player>
            - zap 1 zoltari_interact player:<player> duration:14s
        - flag <player> zoltari_magic_spell
        - flag <player> suspend_clickable:!
    
zoltari_task_08:
    type: task
    script:
    - if <player.has_flag[suspend_clickable].not>:
        - zap 1 zoltari_interact player:<player> duration:14s
        - inject description_inject_task
        - if <player.has_flag[class.type]>:
            - narrate "You are already the class of <player.flag[class.type]>." targets:<player>
            - wait 3s
            - narrate "You must renounce it and seek one of the Arch mages." targets:<player>
        - else:
            - narrate "The path of a mage is long and difficult, and it is ardous to change to a different class once one is chosen." targets:<player>
            - wait 4s
            - narrate "Are you sure?" targets:<player>
            - clickable <npc.name>_task_11 save:<npc.name>_clickable_11 until:30s usages:1 for:<player>
            - narrate  "<element[<&hover[Click to say this to <npc.name>.]><&color[#545454]><&lb><&color[#00cb43]><&sp><&sp><&color[#fbfb54]>1. <&color[#6fd6cb]>Yes, I am sure I want to become a mage.<&color[#545454]><&rb><&end_hover>].on_click[<entry[<npc.name>_clickable_11].command>]>" target:<player>
            - narrate  "<element[<&hover[Click to say this to <npc.name>.]><&color[#545454]><&lb><&color[#00cb43]><&sp><&sp><&color[#fbfb54]>2. <&color[#6fd6cb]>No, not yet.<&color[#545454]><&rb><&end_hover>].on_click[<entry[<npc.name>_clickable_06].command>]>" target:<player>
            - flag <player> zoltari_magic_class
            - zap 9 zoltari_interact player:<player> duration:14s
            - flag <player> suspend_clickable:!

zoltari_task_11:
    type: task
    script:
    - flag <player> class.type:mage
    - flag player stat.points.spell_points.level:+:1
    - narrate <&nl>
    - wait 1s
    - narrate <&nl>
    - narrate format:game_message "You are now the mage class."
    - inject description_inject_task
    - narrate "Welcome to our way of life. I hope I can be of more assistance later down the road."

zoltari_task_09:
    type: task
    script:
    - flag <player> skill.spells.magic_armor.level:+:1
    - inject description_inject_task
    - narrate "Let me teach you something that I know."
    - narrate format:game_message "You have learned the Magic Armor spell. You can upgrade it with skill points in the <element[/spells].bold> menu."

zoltari_task_10:
    type: task
    script:
    - flag <player> skill.spells.magic_missile.level:+:1
    - inject description_inject_task
    - narrate "Let me teach you something that I know."
    - narrate format:game_message "You have learned the Magic Missile spell. You can upgrade it with skill points in the <element[/spells].bold> menu."

zoltari_task_06:
    type: task
    script:
    - random:
        - narrate "Thanks for spending time with this oldtimer." targets:<player>
        - narrate "You know where to find me." targets:<player>
        - narrate "The Plucky Pig Inn will always be here," targets:<player>
        - narrate "Gods be with you." targets:<player>
        - if <player.has_flag[zoltari_adventures_02]>:
            - narrate "Thank you for the whiskey." targets:<player>
    - zap 1 zoltari_interact player:<player> duration:14s


reset_zoltari:
    type: task
    script:
    - flag <player> npc.zoltari.prox_count:!
    - flag <player> zoltari_intro:!
    - flag <player> zoltari_exchange:!
    - flag <player> zoltari_adventures_01:!
    - flag <player> zoltari_adventures_02:!
    - flag <player> zoltari_magic:!
    - flag <player> zoltari_magic_class:!
    - flag <player> zoltari_magic_spell:!